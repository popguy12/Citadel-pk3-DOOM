Class Citadel_Trooper_CavalryRifle : Citadel_MonsterBase
{
		Default
		{
			Radius 16;
			Height 56;
			Speed 4;
			FastSpeed 6;
			Mass 100;
			Health 50;
			GibHealth -40;
			PainChance 256;
			Scale 0.12; //Make sure to adjust the values in the See state to match these
			BloodColor "Red";
			
			Citadel_MonsterBase.AttackRange 1600; //32 Dmu (1 meter) * 50 (55 yards effective range of Luger)
			Citadel_MonsterBase.CanIRoll false;
			SeeSound "Nazi/Generic/sight";
			PainSound "Nazi/Generic/pain";
			DeathSound "Nazi/Generic/death";
			ActiveSound "Nazi/Generic/sight";
			
			DropItem "Citadel_KarAmmo", 255, 8;
			DropItem "Citadel_Kar98K", 100, 1;
			
			Obituary "$OB_ZOMBIE";
		}
		
		int HowManyGrenadesHaveIThrown;
		
		void FireProjBullets()
		{
			A_Light(2);
			//A_SpawnProjectile("Citadel_Kar98Bullets", 32, 0, (frandom(3,-3)), CMF_AIMDIRECTION, self.pitch + (frandom(3,-3)));
			Citadel_FireMonsterBullet("Citadel_Enemy_127x99mm");
			A_StartSound("Kar98/Fire", CHAN_AUTO, CHANF_OVERLAP);
			AmmoInMag--;
		}
		
		void FireProjGren()
		{
			//A_Spawnprojectile("Citadel_SmokeGrenade", 32, 0, 0, CMF_ABSOLUTEPITCH, self.pitch-5);
			HowManyGrenadesHaveIThrown++;
		}
		
		override void PostBeginPlay()
		{
			Super.PostBeginPlay(); // call the super function for virtual functions so we don't break shit if GZdoom update.
		}
		
		override void BeginPlay()
		{
			super.BeginPlay();
			AmmoInMag = random(3,11); 
		}
		
		override void Tick()
		{
			Super.Tick();
		}
		
		States
		{
		
		Spawn:
			TRAA A 1;
			TNT1 A 0;
		Stand:
			TRAA AAAA 5
			{
				A_LookEx();
				A_SetScale(scale.X,Scale.Y+0.001);
			}
			TRAA AAAA 5
			{
				A_LookEx();
				A_SetScale(scale.X,Scale.Y-0.001);
			}
			Loop;
		See:
			TNT1 A 0
			{
				A_SetScale(0.12,0.12);
				EnemyLastSighted = Level.MapTime;
				if(AmmoInMag < 4)
				{
					bFRIGHTENED = true;
				}
				else
				{
					bFRIGHTENED = false;
				}
			}
		SeeContinue:
			TRAA BBBBCCCC 1 AI_SmartChase();
			TNT1 A 0 A_Fallback();
			TRAA DDDDEEEE 1 AI_SmartChase();
			TNT1 A 0 A_Fallback();
			Loop;
		FallBack:
			TNT1 A 0 A_Jump(255, "Fallback1", "Roll", "See");
		FallBack1:
			TRAA E 3 {
				A_FaceTarget(10);
				A_Recoil(2);
				return A_Jump(64,"Missile");
			}
			TRAA D 3 {
				A_FaceTarget(10);
				A_Recoil(2);
				return A_Jump(64,"Missile");
			}
			TRAA C 3 {
				A_FaceTarget(10);
				A_Recoil(2);
				return A_Jump(64,"Missile");
			}
			TRAA B 3 {
				A_FaceTarget(10);
				A_Recoil(2);
				return A_Jump(64,"Missile");
			}
			TRAA E 3 {
				A_FaceTarget(10);
				A_Recoil(2);
				return A_Jump(64,"Missile");
			}
			TRAA D 3 {
				A_FaceTarget(10);
				A_Recoil(2);
				return A_Jump(64,"Missile");
			}
			TRAA C 3 {
				A_FaceTarget(10);
				A_Recoil(2);
				return A_Jump(64,"Missile");
			}
			TRAA B 3 {
				A_FaceTarget(10);
				A_Recoil(2);
			}
			Goto Missile;
		
		////////////////
		//Attack Logic// 
		////////////////
		Melee:
			TRAA A 1 A_CheckLOF(1);
			Goto See;
			TRAA A 1 A_FaceTarget(45, 45, 0, 0, FAF_MIDDLE);
			TRAA C random(10,20);
			TRAA C 3
			{
				//Melee attack
				A_StartSound("Fists/Swing");
				A_CustomMeleeAttack(random(10, 30), "Fists/HitFlesh");
			}
			WBPN G 3;
			TRAA E 6;
			Goto See;
		Missile:
			TNT1 A 0 A_JumpIf(AttackDelay > 3, "See");
			TRAA A 1 A_CheckLOFRanged("AttackHandler", "Roll");
			Goto See;
		AttackHandler:
			TNT1 A 0
			{
				int chance = (random(1,256));
				
				if((chance > 232) && (HowManyGrenadesHaveIThrown < 4))
				{
					return A_Jump(256, "Grenade");
				}
				
				if(AmmoInMag <= 0)
				{
					return A_Jump(256,"Reload");
				}
				
				AttackDelay = AttackDelay + 20;
				
				return A_Jump(256, "Attack1");
			}
		
		Attack1:
			TNT1 A 0
			{
				if(AmmoInMag <= 0)
				{
					A_Jump(256,"Reload");
				}
			}
			TRAA AFGH 4 A_FaceTarget(45, 45, 0, 0, FAF_MIDDLE);
			TRAA I random(5,20) A_FaceTarget(45, 45, 0, 0, FAF_MIDDLE);
			TRAA J 2 BRIGHT FireProjBullets;
			TRAA IH 4;
			TRAA G 4;
			TRAA G 24 A_StartSound("Kar98/BoltOpen", 8, CHANF_OVERLAP, attenuation: 2);
			TRAA G 24 A_StartSound("Kar98/BoltClose", 8, CHANF_OVERLAP, attenuation: 2);
			TRAA G 4;
			TNT1 A 0 A_Jump(90, "Attack1");
			TRAA FA 4;
			Goto See;
			
		Grenade:
			TNT1 A 0 A_JumpIfCloser(500, 1);
			Goto Attack1;
			TNT1 A 0 A_JumpIfCloser(90, "Attack1");
		ThrowGrenade:
			TNT1 A 0; //Grenade sound
			TRAA G 6
			{
				A_ActiveSound();
				A_FaceTarget(90,45);
			}
			TRAA H 6;
			TRAA I 6
			{
				A_FaceTarget(90,45);
				FireProjGren();
			}
			TRAA G 6;
			Goto See;
		Reload:
			TRAA G 6;
			TRAA O 24 A_StartSound("Kar98/BoltOpen", 8, CHANF_OVERLAP, attenuation: 2);
			TRAA M 16;
			TNT1 A 0
			{
				A_StartSound("Kar98/ClipLoad", 8, CHANF_OVERLAP, attenuation: 2);
				AmmoInMag = 11;
			}
			TRAA NO 12;
			TRAA O 6;
			TRAA G 24 A_StartSound("Kar98/BoltClose", 8, CHANF_OVERLAP, attenuation: 2);
			TRAA E 2;
			Goto See;

		////////////////
		//Pain Logic// 
		////////////////
		Pain:
			TNT1 A 0 A_JumpIf(kickeddown, "KickedPain");
			WBPN G 6 A_Pain();
			Goto See;
		Death:
			TNT1 A 0
			{
				A_Scream();
				A_NoBlocking();
			}
			WBPN IJKL 3;
			WBPN M -1;
			Stop;
		XDeath:
			TNT1 A 0
			{
				A_XScream();
				A_NoBlocking();
			}
			WBPN IJKL 3;
			WBPN M -1;
			Stop;
		Raise:
			WBPN MLKJIG 3;
			Goto Spawn;
		Pain.Kick:
			TNT1 A 0
			{
				kickeddown = true;
				A_Pain();
				A_ChangeVelocity(0, 0, 5, CVF_RELATIVE);
			}
			WBNK A 3;
		KickedLoop:
			WBNK A 1 A_CheckFloor("Kicked");
			Loop;
		KickedPain:
			WBNK B 10 A_Pain();
		Kicked:
			TNT1 A 0 A_CheckFloor(1);
			Goto KickedLoop;
			TNT1 A 0;
			WBNK BC 10;
			WBNK C random(25,50);
			WBNK D 10;
			TNT1 A 0
			{
				kickeddown = false;
				A_ActiveSound();
			}
			Goto See;
		
		////////////////////
		//Generic By Actor//
		////////////////////
		
		Roll:
			TNT1 A 0 A_Jump(256, "SHR", "SHL", "See");
		SHR:
			TRAA A 3 A_FaceTarget;
			TRAA E 3
			{
				A_FaceTarget();
				A_ChangeVelocity(frandom(5,-5), -8, 0, CVF_RELATIVE);
			}
			TRAA E 24;
		SHRL:
			TRAA E 1 A_CheckFloor("SHRE");
			TRAA E 1;
			Loop;
		SHRE:
			TRAA E 1 A_FaceTarget;
			TNT1 A 0 A_Stop();
			TNT1 A 0 A_Jump(256, "See", "Missile");
			Goto See;
		SHL:
			TRAA A 3 A_FaceTarget;
			TRAA E 3
			{
				A_FaceTarget();
				A_ChangeVelocity(frandom(5,-5), 8, 0, CVF_RELATIVE);
			}
			TRAA E 24;
		SHLL:
			TRAA E 1 A_CheckFloor("SHLE");
			TRAA E 1 A_CheckCeiling("SHLE");
			Loop;
		SHLE:
			TRAA E 1 A_FaceTarget;
			TNT1 A 0 A_Stop();
			TNT1 A 0 A_Jump(256, "Roll", "See", "Missile");
			Goto See;
	}
}