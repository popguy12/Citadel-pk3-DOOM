class Citadel_MetaMagnum : CitadelWeapon
{
 	Default
	{		
		Weapon.AmmoType "Clip";
		Weapon.AmmoUse 0;
		Weapon.AmmoGive 20;
		Weapon.AmmoType2 "Citadel_MetaMagnum_Mag";
		Weapon.AmmoUse2 0;
		Weapon.AmmoGive2 0;
		
		Weapon.SlotNumber 2;
		
		Obituary "$OB_MPPISTOL";
		Inventory.Pickupmessage "MetaMagnum Mk. II";
		Tag "MetaMagnum Mk. II";
		Scale 0.16;
		Inventory.PickupSound "weapons/pistol/pickup";
		+WEAPON.NOALERT;
		+WEAPON.AMMO_OPTIONAL;
		+WEAPON.NOAUTOFIRE;
		
		CitadelWeapon.PlaySpeed 1.4;
		CitadelWeapon.SlotType 2;
		CitadelWeapon.InfoGraphics "ISMEDI", "ICUSSWN", "ICUSSWU", "WLMEDI";
	}
	
	action void FireWeapon()
	{
		A_AlertMonsters();
		A_Overlay(-3, "MuzzleSmall");
		A_OverlayScale(-3, 4, 4);
		A_OverlayFlags(-3, PSPF_ALPHA, true);
		A_OverlayFlags(-3, PSPF_RENDERSTYLE, true);
		A_OverlayRenderstyle(-3, STYLE_ADD);
		
		A_StartSound("MetaMagnum/Fire", 0, CHANF_OVERLAP, 1);
		A_StartSound("MetaMagnum/FireAdd", 0, CHANF_OVERLAP, 0.5);
		A_StartSound("MetaMagnum/FireBass", 0, CHANF_OVERLAP, 0.75);
		Citadel_FireBullets("Citadel_44Magnum", 1, frandom(-0.1, 0.1), 0, 0, frandom(-0.1, 0.1));
		A_SpawnItemEx("PlayerMuzzleFlash",30,0,45);
		
		//this handles placing the flash correctly
		if(CountInv("AimingToken"))
		{
			A_OverlayOffset(-3, 0 - 80, 32 - 256);
			Citadel_HandleWeaponFeedback(2, 3, -0.20, frandom(+0.15, -0.15), 0, 0, 2);
			A_ZoomFactor(1.5-0.01);
		}
		else
		{
			A_OverlayOffset(-3, 0 - 80, 32 - 480);
			Citadel_HandleWeaponFeedback(2, 3, -0.40, frandom(+0.30, -0.30), 0, 0, 2);
			A_ZoomFactor(1-0.01);
		}
		//gunsmoke
		A_TakeInventory("Citadel_MetaMagnum_Mag", 1);
	}
	
	bool ReloadingCurrently;
	int ReloadingTimer;
	
	States
	{
	
	Deselect:
		TNT1 AAA 1;
		PSTG A 0 A_Lower(25);
		Wait;
	User4:
		TNT1 A 0 A_StartSound("uni/ClothFoleyS", 0, CHANF_OVERLAP, 1);
		TNT1 AAA 1;
		Goto Ready;
	User2:
		TNT1 A 0 A_StartSound("uni/ClothFoleyS", 0, CHANF_OVERLAP, 1);
		TNT1 AAA 1;
		Goto Ready;
	User3:
		TNT1 A 0;
		Goto KnifeAttack;
	Select:
		TNT1 A 0
		{
			A_TakeInventory("AimingToken");
		}
		TNT1 A 0 A_Raise(25);
		Wait;
	Ready:
		TNT1 A 0;
		TNT1 A 0 A_StartSound("uni/ClothFoleyS", 0, CHANF_OVERLAP, 1);
		TNT1 AAA 1;
	WeaponReady:
		#### # 0 A_JumpIfInventory("AimingToken", 1, "WeaponReady2");
		MM2B A 1 Citadel_WeaponReady(WRF_ALLOWRELOAD | WRF_ALLOWUSER2 | WRF_ALLOWUSER3 | WRF_ALLOWUSER4);
		TNT1 A 0; //A_JumpIfInventory("ThrowGrenade", 1, "FragGrenade");
		TNT1 A 0; //A_JumpIfInventory("ThrowBang", 1, "StunGrenade");
		Loop;
	WeaponReady2:
		MM2A A 1 Citadel_WeaponReady();
		TNT1 A 0; //A_JumpIfInventory("ThrowGrenade", 1, "FragGrenade");
		TNT1 A 0; //A_JumpIfInventory("ThrowBang", 1, "StunGrenade");
		Loop;
	Fire:
		#### # 0 A_JumpIfInventory("AimingToken", 1, "Fire2");
		#### # 0 A_JumpIf(CountInv("Citadel_MetaMagnum_Mag") == 0, "DryFire");
		MM2B A 1 FireWeapon();
		TNT1 A 0 A_ZoomFactor(1);
		MM2B BC 1;
		//TNT1 A 0 A_JumpIf(CountInv("Citadel_MetaMagnum_Mag") == 0, "WeaponReadyEmpty");
		MM2B DEF 1;
		MM2B GH 1 Citadel_WeaponReady(WRF_ALLOWRELOAD | WRF_ALLOWUSER2 | WRF_ALLOWUSER3 | WRF_ALLOWUSER4);
		Goto WeaponReady;
	Fire2:
		#### # 0 A_JumpIf(CountInv("Citadel_MetaMagnum_Mag") == 0, "DryFire");
		MM2A A 1 FireWeapon();
		TNT1 A 0 A_ZoomFactor(1.5);
		MM2A BC 1;
		//TNT1 A 0 A_JumpIf(CountInv("Citadel_MetaMagnum_Mag") == 0, "WeaponReadyEmpty");
		MM2A CB 1;
		MM2A A 1 Citadel_WeaponReady(WRF_ALLOWRELOAD | WRF_ALLOWUSER2 | WRF_ALLOWUSER3 | WRF_ALLOWUSER4);
		Goto WeaponReady2;
	AltFire:
		TNT1 A 0 A_JumpIfInventory("AimingToken", 1, "AltFire2");
		TNT1 A 0 A_StartSound("uni/clothfoleys", 0, CHANF_OVERLAP);
		TNT1 A 0 A_GiveInventory("AimingToken");
		TNT1 A 0 A_ZoomFactor(1.5);
		MM2B A 4;
		Goto WeaponReady2;
	AltFire2:
		TNT1 A 0 A_StartSound("uni/clothfoleys", 0, CHANF_OVERLAP);
		TNT1 A 0 A_TakeInventory("AimingToken");
		TNT1 A 0 A_ZoomFactor(1);
		MM2A A 4;
		Goto WeaponReady;
	
	Reload:
		TNT1 A 0;
		TNT1 A 0 A_JumpIf(CountInv("Citadel_MetaMagnum_Mag") == 7 || CountInv("Clip") == 0, "WeaponReady");
		TNT1 A 0 A_JumpIf(invoker.ReloadingCurrently, "ReloadRepeat");
		TNT1 A 0
		{
			invoker.ReloadingCurrently  = true;
		}
		TNT1 A 0 A_JumpIf(CountInv("Citadel_MetaMagnum_Mag") == 0, "ReloadChamber");
		MM2B A 1;
	ReloadRepeat:
		MM2C ABCD 1;
		TNT1 A 0
		{
			Citadel_AmmoIntoMag("Citadel_MetaMagnum_Mag", "Clip", (CountInv("Citadel_MetaMagnum_Mag") + 1), 1);
			invoker.ReloadingTimer = 70;
			A_StartSound("MetaMagnum/load", 0, CHANF_OVERLAP, 1);
		}
		MM2C EFG 1;
		TNT1 A 0 A_Jumpif(CountInv("Clip") == 0, "ReloadEnd");
		TNT1 A 0 A_Jumpif(CountInv("Citadel_MetaMagnum_Mag") == 7, "ReloadEnd");
		MM2C CBA 1 Citadel_WeaponReady(WRF_ALLOWRELOAD | WRF_NOFIRE | WRF_DISABLESWITCH);
	ReloadButtonLoop:
		MM2C A 1
		{
			Citadel_WeaponReady(WRF_ALLOWRELOAD | WRF_NOFIRE | WRF_DISABLESWITCH);
			invoker.ReloadingTimer--;
		}
		TNT1 A 0 A_JumpIf(invoker.ReloadingTimer <= 0, "ReloadEnd");
		Loop;
	ReloadEnd:
		MM2C CBA 1;
		MM2C A 1
		{
			invoker.ReloadingCurrently = false;
			invoker.ReloadingTimer = 0;
		}
		MM2B A 1;
		Goto WeaponReady;
	ReloadChamber:
		MM2B A 1;
		MM2C HIJKLM 1;
		MM2C N 1
		{
			Citadel_AmmoIntoMag("Citadel_MetaMagnum_Mag", "Clip", (CountInv("Citadel_MetaMagnum_Mag") + 1), 1);
			invoker.ReloadingTimer = 70;
			A_StartSound("MetaMagnum/load", 0, CHANF_OVERLAP, 1);
		}
		MM2C OPQRSTUV 1;
		Goto ReloadButtonLoop;
		
	DryFire:
		#### # 1;
		Goto WeaponReady;
	
 	Spawn:
		B005 C -1;
		Stop;
	
	}
}

Class Citadel_MetaMagnum_Mag : Ammo
{
	Default
	{
		Inventory.Amount 0;
		Inventory.MaxAmount 7;
		Ammo.BackpackAmount 0;
		Ammo.BackpackMaxAmount 7;
	}
}